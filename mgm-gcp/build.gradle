buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.openapitools:openapi-generator-gradle-plugin:5.3.0"
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:2.4.2'
        classpath 'gradle.plugin.com.github.johnrengelman:shadow:7.1.2'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
    id 'java'
}

apply plugin: 'application'
apply plugin: 'org.openapi.generator'
apply plugin: 'com.google.cloud.tools.appengine'
apply plugin: 'com.github.johnrengelman.shadow'

group 'uk.co.cerihughes'
version '1.0.0'

openApiGenerate {
    inputSpec = "../mgm-schema/v1/openapi.yaml"
    outputDir = "${project.projectDir}/src/gen/openapi"
    generatorName = "java"
    generateApiTests = false
    generateModelTests = false
    configOptions = [
            dateLibrary: "java8",
            library: "resteasy",
            apiPackage: "uk.co.cerihughes.mgm.api",
            invokerPackage: "uk.co.cerihughes.mgm.invoker",
            modelPackage: "uk.co.cerihughes.mgm.model.output"
    ]
}

shadowJar {
    mergeServiceFiles()
}

sourceSets {
    main {
        java.srcDirs += "src/main/webapp"
        java.srcDirs += "src/gen/openapi/src/main/java"
    }
}

sourceCompatibility = 11
targetCompatibility = 11
mainClassName = "uk.co.cerihughes.mgm.Main"

repositories {
    mavenCentral()
}

ext {
    jettyVersion = '9.4.44.v20210927'
    resteasyVersion = '4.5.11.Final'
    jacksonVersion = '2.10.5'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"

    implementation "org.eclipse.jetty:jetty-server:${jettyVersion}"
    implementation "org.eclipse.jetty:jetty-servlet:${jettyVersion}"
    implementation "org.eclipse.jetty:jetty-webapp:${jettyVersion}"

    implementation "org.jboss.resteasy:resteasy-client:${resteasyVersion}"
    implementation "org.jboss.resteasy:resteasy-multipart-provider:${resteasyVersion}"
    implementation "org.jboss.resteasy:resteasy-jackson2-provider:${resteasyVersion}"
    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"

    implementation 'io.swagger:swagger-annotations:1.5.22'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.1'

    implementation 'com.google.apis:google-api-services-sheets:v4-rev20210629-1.32.1'
    implementation 'se.michaelthelin.spotify:spotify-web-api-java:7.0.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

// Deploy with ./gradlew clean appengineDeploy
appengine {
    deploy {
        projectId = 'GCLOUD_CONFIG'
        version = 'GCLOUD_CONFIG'
        stopPreviousVersion = false
        promote = false
    }
    stage {
        artifact = "${buildDir}/libs/${project.name}-${version}-all.jar"
    }
}

test {
    useJUnitPlatform()
}
